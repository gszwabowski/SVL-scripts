#svl
//Commentary by Greg Szwabowski 12/4/2020
//This script will allow you to generate .png images of protein-ligand interactions for
// all docked poses inside a database, where 'mdb' is the name of your database.

function DrawLigandInteractions;
global function pli_gen [mdb];

Close [force:1, viewreset:1, delgobj:1];  // close any open structures
local entry;
local entries = db_Entries mdb;

for entry in entries loop
		local entry_num = indexof [entry, entries];
		local mseq = db_ReadFields [mdb, entry, 'mseq'];
		local [ligand] =db_ReadFields [mdb, entry, 'mol']; //get structure from fieldname
		local [receptor] =db_ReadFields [mdb, entry, 'receptor'];
		local [receptor_chain, receptor_name] =db_CreateMolecule receptor;
		local [ligand_chain, ligand_name] =db_CreateMolecule ligand;
		
		local [rec_atoms] = cAtoms receptor_chain; // get receptor atoms
		local [lig_atoms] = cAtoms ligand_chain;
		gr_fwrite [ 
			tok_cat [fbase mdb, '_', 'entry_', totok entry_num, '_', 'mseq_', totok mseq, '_', totok ligand_name, '.png'], 
			DrawLigandInteractions [[lig_atoms], [rec_atoms], []], 
			'image/png', 
			[], 
			[] 
		];
		
		oDestroy receptor_chain;
		oDestroy ligand_chain;
endloop

write 'Done.\n';

endfunction	
