function ph4_EditorWkeyList;
function ph4_EditorGetData;
function fwrite_PH4;
global function sample_ph4_combinations_nminus1 [features]
//Commentary 5/27/21 GS
//This script will create a folder for partial ph4 files, cd into it, then create "partial" pharmacophores
//by sampling combinations of n-1 pharmacophore features. For example, using this with a directory containing
//7 feature pharmacophores will create 7 partial pharmacophores with 6 features.
//
//ARGUMENTS: 
//features: number of features in a ph4

Close [force:1, viewreset:1, delgobj:1];  // close any open structures
fmkdir 'partial_ph4s';
cd 'partial_ph4s/';
local ph4, i;
local ph4_files = flist [['..'],'*.ph4'];

for ph4 in ph4_files loop
	for i = 1, features, 1 loop
		//print i;
		run ['$MOE/svl/run/ph4_edit_2.svl', ph4];
		local wkey = ph4_EditorWkeyList [];
		local data = ph4_EditorGetData wkey;
		local feature_types = data.F.expr;
		local nvp = WindowValues wkey;
		local mainlist = nvp.mainlist(1);
		WindowTrigger [wkey, [mainlist:i]];
		WindowTrigger [wkey, [button_delete:1024]];
		data = ph4_EditorGetData wkey;
		fwrite_PH4 [tok_cat [tok_drop [fbase ph4, 3], '_', totok i, '.ph4'], data, [header:1]];
		sleep 1.5;
		if wkey == ph4_EditorWkeyList [] then
			WindowDestroy wkey;
		endif;	
	endloop
endloop

cd '..';
write 'Done.\n';

endfunction
